#version 450 core

struct Vertex {
    vec4 position;
    vec4 normal;
};

struct Ray {
    vec3 origin;
    vec3 dir;
};

struct Material {
    vec4 diffuse;
    vec4 emissive;
};

struct Mesh {
    uint vertexOffset;
    uint numVertices;
    uint materialId;
};

struct IntersectionInfo {
    float t;
    vec3 normal;
    uint materialId;
};

const float EPSILON = 1e-4;

layout (local_size_x = 1, local_size_y = 1) in;
layout (rgba32f, binding = 0) uniform image2D oImage;
layout (std430, binding = 1) buffer vertexBuffer
{
    Vertex vertices[];
};
layout (std430, binding = 2) buffer materialBuffer
{
    Material materials[];
};
layout (std430, binding = 3) buffer meshBuffer
{
    Mesh meshes[];
};

uniform vec2 uResolution;
uniform vec2 uOffset;

uniform struct {
    vec3 position;
    vec3 lowerLeft;
    vec3 upperRight;
} uCamera;

uniform int uFrameNumber;

uint rngState = 0;

// https://www.burtleburtle.net/bob/hash/integer.html
uint wangHash() {
    rngState = (rngState ^ 61u) ^ (rngState >> 16);
    rngState = rngState + (rngState << 3);
    rngState = rngState ^ (rngState >> 4);
    rngState = rngState * 0x27d4eb2d;
    rngState = rngState ^ (rngState >> 15);
    return rngState;
}

// Returns a float in range [0, 1)
float random() {
    return float(wangHash()) / 4294967296.0;
}

// NOTE: very very small chance this is a zero vector
vec3 randomUnitVector() {
    return normalize(vec3(random() * 2 - 1, random() * 2 - 1, random() * 2 - 1));
}

// https://en.wikipedia.org/wiki/SRGB
float srgbToLinearComponent(float x) {
	if (x <= 0.04045) return x / 12.92;
	return pow((x + 0.055) / 1.055, 2.4);
}

vec3 srgbToLinear(vec3 x) {
	return vec3(
		srgbToLinearComponent(x.x),
		srgbToLinearComponent(x.y),
		srgbToLinearComponent(x.z)
	);
}

float linearToSrgbComponent(float x) {
    if (x <= 0.0031308) return 12.92 * x;
    return 1.055 * pow(x, 1/2.4) - 0.055;
}

vec3 linearToSrgb(vec3 x) {
    return vec3(
        linearToSrgbComponent(x.x),
        linearToSrgbComponent(x.y),
        linearToSrgbComponent(x.z)
    );
}

// Möller–Trumbore intersection algorithm
IntersectionInfo intersectTriangle(Ray ray, Vertex v0, Vertex v1, Vertex v2, uint materialId) {
    IntersectionInfo isect;
    isect.materialId = materialId;
    isect.t = -1;

    vec3 e1 = v1.position.xyz - v0.position.xyz;
    vec3 e2 = v2.position.xyz - v0.position.xyz;
    vec3 h = cross(ray.dir, e2);
    float a = dot(e1, h);

    // check if ray is parallel to the triangle
    if (a > -EPSILON && a < EPSILON) {
        return isect;
    }

    // calculate u
    float f = 1.0 / a;
    vec3 s = ray.origin - v0.position.xyz;
    float u = f * dot(s, h);

    // check if u is on triangle
    if (u < 0.0 || u > 1.0) {
        return isect;
    }

    // calculate v
    vec3 q = cross(s, e1);
    float v = f * dot(ray.dir, q);

    // check if v is on triangle
    if (v < 0.0 || u + v > 1.0) {
        return isect;
    }

    // get intersection
    float t = f * dot(e2, q);
    if (t > EPSILON) {
        isect.t = t;

        isect.normal = normalize((u * v0.normal.xyz) + (v * v1.normal.xyz) + ((1 - u - v) * v2.normal.xyz));
    }

    return isect;
}

IntersectionInfo intersect(Ray ray) {
    IntersectionInfo nearest;
    nearest.t = -1;

    // Iterate over meshes
    for (uint i = 0; i < meshes.length(); ++i) {
        // Get range of vertices
        uint first = meshes[i].vertexOffset;
        uint last = first + meshes[i].numVertices;

        for (uint j = first; j < last; j += 3) {
            IntersectionInfo current = intersectTriangle(ray, vertices[j + 0], vertices[j + 1], vertices[j + 2], meshes[i].materialId);
            if (current.t >= 0 && (current.t < nearest.t || nearest.t < 0)) {
                nearest = current;
            }
        }
    }

    return nearest;
}

vec3 trace(Ray ray) {
    vec3 color = vec3(0);
    vec3 throughput = vec3(1);

    const uint numBounces = 5;
    for (int i = 0; i < numBounces; ++i) {
        IntersectionInfo isect = intersect(ray);

        if (isect.t < 0) {
            // We miss, return sky color
            color += throughput * mix(vec3(1), vec3(0.5, 0.7, 1.0), ray.dir.y * 0.5 + 0.5);
            break;
        }

        Material mat = materials[isect.materialId];

        // New ray
        ray.origin += ray.dir * isect.t;
        ray.dir = normalize(isect.normal + randomUnitVector());

        // TODO: do math correctly

        color += mat.emissive.xyz * throughput;
        throughput *= mat.diffuse.xyz;
    }

    return color;
}

void main() {
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy) + ivec2(uOffset);

    // seed rng
    // TODO: better random numbers
    rngState = uint(uint(coord.x) * 1973u + uint(coord.y) * 9277u + uint(uFrameNumber) * 26699u) | 1u;

    // anti-aliasing jitter
    vec2 jitter = vec2(random(), random());
    vec2 uv = vec2(coord + jitter) / uResolution;

    // generate ray
    Ray ray;
    ray.origin = uCamera.position;
    ray.dir    = normalize(mix(uCamera.lowerLeft, uCamera.upperRight, vec3(uv, 0)) - ray.origin);

    // get color from previous frame, change color space to linear
    vec3 color = srgbToLinear(imageLoad(oImage, coord).rgb) * uFrameNumber;

    // trace
    color += trace(ray);

    // average
    color /= float(uFrameNumber + 1);

    // output
    imageStore(oImage, coord, vec4(linearToSrgb(color), 1));
}
